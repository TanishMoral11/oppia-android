apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion target_sdk_version
  buildToolsVersion build_tools_version
  defaultConfig {
    applicationId "org.oppia.android"
    minSdkVersion min_sdk_version
    targetSdkVersion target_sdk_version
    versionCode 1
    versionName "1.0"
    multiDexEnabled true
    testInstrumentationRunner "org.oppia.android.testing.OppiaTestRunner"
    // https://developer.android.com/training/testing/junit-runner#ato-gradle
    testInstrumentationRunnerArguments clearPackageData: 'true'
    javaCompileOptions {
      annotationProcessorOptions {
        includeCompileClasspath true
      }
    }
    vectorDrawables { useSupportLibrary true }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8
    useIR = true
    freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
  }
  buildFeatures {
    compose true
  }
  composeOptions {
    kotlinCompilerExtensionVersion compose_version
    kotlinCompilerVersion kotlin_version
  }
  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
    debug {
      pseudoLocalesEnabled true
    }
  }
  splits {
    // See: https://developer.android.com/studio/build/configure-apk-splits
    density {
      enable false
    }
  }
  dataBinding {
    enabled = true
  }
  testOptions {
    // https://developer.android.com/training/testing/junit-runner#ato-gradle
    execution 'ANDROIDX_TEST_ORCHESTRATOR'
    unitTests {
      includeAndroidResources = true
      all {
        maxHeapSize = "4096m"

        // Enable forking to ensure each test is properly run in isolation. For context, see:
        // https://discuss.gradle.org/t/36066/2 & https://github.com/oppia/oppia-android/issues/1942
        forkEvery = 1
        maxParallelForks = Runtime.getRuntime().availableProcessors()

        // https://discuss.gradle.org/t/29495/2 & https://stackoverflow.com/a/34299238.
        testLogging {
          events "passed", "skipped", "failed"
          showExceptions = true
          exceptionFormat = "full"
          showCauses = true
          showStackTraces = true
          showStandardStreams = false
        }
      }
    }
    animationsDisabled = true
  }

  // https://proandroiddev.com/isolated-fragments-unit-tests-that-run-both-instrumented-and-on-the-jvm-with-the-same-source-code-283db2e9be5d
  sourceSets {
    androidTest {
      java.srcDirs += "src/sharedTest/java"
      kotlin.srcDirs += "src/sharedTest/java"
    }
    test {
      java.srcDirs += "src/sharedTest/java"
      kotlin.srcDirs += "src/sharedTest/java"
      java.exclude('**/DataBinderMapperImpl.java') // Bazel-specific file used to run tests
    }
  }
}

// See notice for the excluded files in domain/build.gradle for an explanation on the
// language-related exclusions. The additional application component is excluded because Gradle
// isn't able to compile multiple Dagger graphs in the same build configuration.
def filesToExclude = [
    '**/*AppLanguageLocaleHandlerTest*.kt',
    '**/*AppLanguageResourceHandlerTest*.kt',
    '**/*AppLanguageWatcherMixinTest*.kt',
    '**/*ActivityLanguageLocaleHandlerTest*.kt'
]
_excludeSourceFiles(filesToExclude)

// Check if sharding is being attempted and, if so, exclude all tests that aren't in the specified
// shard. The approach to using arguments was inspired by
// https://prandroid.dev/Parallel-unit-tests-in-android/.

// NOTE TO DEVELOPER: You likely will need to clean before running any of these, and you can't run
// one shard after another without cleaning first (since Gradle doesn't handle file exclusion well,
// it expects files to be present corresponding to generated code, such as Dagger, from previous
// runs).
def appModuleShardCount = 8
def shardIndexes = 0..(appModuleShardCount - 1)
shardIndexes.each { shardIndex ->
  if (project.gradle.startParameter?.taskRequests?.args[0]?.remove("--shard$shardIndex".toString())) {
    def filesToExcludeForShard = _computeTestsCorrespondingToShard(appModuleShardCount, shardIndex, true)
    println("Excluding ${filesToExcludeForShard.size} tests for shard $shardIndex")
    _excludeSourceFiles(filesToExcludeForShard.collect {
      // Ensure the test paths are properly relative to the app module so that their exclusion
      // filters match (but keep the unique relative paths to avoid accidentally skipping tests that
      // share names). Note that the 'org/oppia/android/app' part is specifically included to ensure
      // that cases where a path relative to org/oppia/android/app is fully overlapping with another
      // path result in one of the tests being ignored in all shards.
      def prefixToExclude = "org/oppia/android/app/"
      "**/${it.substring(it.indexOf(prefixToExclude))}"
    })
  }
}
if (project.gradle.startParameter?.taskRequests?.args[0]?.remove("--list-shards")) {
  println()
  println("The app module supports $appModuleShardCount shards")
  println()
  shardIndexes.each { shardIndex ->
    def filesInShard = _computeTestsCorrespondingToShard(appModuleShardCount, shardIndex, false).sort()
    println("Shard $shardIndex has ${filesInShard.size} tests:")
    filesInShard.each { filePath ->
      println("- $filePath")
    }
    println()
  }
  // This is a hacky way to make an 'information' command.
  _excludeSourceFiles("**/*Test.kt")
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation(
      "androidx.appcompat:appcompat:$androidx_appcompat_appcompat",
      "androidx.compose.foundation:foundation:$androidx_compose_foundation_foundation",
      "androidx.compose.foundation:foundation-layout:$androidx_compose_foundation_foundation_layout",
      "androidx.compose.material:material:$androidx_compose_material_material",
      "androidx.compose.runtime:runtime:$androidx_compose_runtime_runtime",
      "androidx.compose.runtime:runtime-livedata:$androidx_compose_runtime_runtime_livedata",
      "androidx.compose.ui:ui:$androidx_compose_ui_ui",
      "androidx.compose.ui:ui-tooling:$androidx_compose_ui_ui_tooling",
      "androidx.constraintlayout:constraintlayout:$androidx_constraintlayout_constraintlayout",
      "androidx.core:core-ktx:$androidx_core_core_ktx",
      "androidx.legacy:legacy-support-v4:$androidx_legacy_legacy_support_v4",
      "androidx.lifecycle:lifecycle-extensions:$androidx_lifecycle_lifecycle_extensions",
      "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle_lifecycle_runtime_ktx",
      "androidx.navigation:navigation-fragment:$androidx_navigation_navigation_fragment",
      "androidx.navigation:navigation-fragment-ktx:$androidx_navigation_navigation_fragment_ktx",
      "androidx.navigation:navigation-ui:$androidx_navigation_navigation_ui",
      "androidx.navigation:navigation-ui-ktx:$androidx_navigation_navigation_ui_ktx",
      "androidx.fragment:fragment:$androidx_fragment_fragment",
      "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle_lifecycle_runtime_ktx",
      "androidx.multidex:multidex:$androidx_multidex_multidex",
      "androidx.recyclerview:recyclerview:$androidx_recyclerview_recyclerview",
      "androidx.work:work-runtime-ktx:$androidx_work_work_runtime_ktx",
      "com.github.bumptech.glide:glide:$com_github_bumptech_glide_glide",
      "com.google.android.flexbox:flexbox:$com_google_android_flexbox_flexbox",
      "com.google.android.material:material:$com_google_android_material_material",
      "com.google.android.material:compose-theme-adapter:$com_google_android_material_compose_theme_adapter",
      "com.google.dagger:dagger:$com_google_dagger_dagger",
      "com.google.firebase:firebase-analytics:$com_google_firebase_firebase_analytics",
      "com.google.firebase:firebase-analytics-ktx:$com_google_firebase_firebase_analytics_ktx",
      "com.google.firebase:firebase-appcheck:$com_google_firebase_firebase_appcheck",
      "com.google.firebase:firebase-appcheck-debug:$com_google_firebase_firebase_appcheck_debug",
      "com.google.firebase:firebase-appcheck-playintegrity:$com_google_firebase_firebase_appcheck_playintegrity",
      "com.google.firebase:firebase-core:$com_google_firebase_firebase_core",
      "com.google.firebase:firebase-crashlytics:$com_google_firebase_firebase_crashlytics",
      "com.google.firebase:firebase-firestore-ktx:$com_google_firebase_firebase_firestore_ktx",
      "com.google.firebase:firebase-auth-ktx:$com_google_firebase_firebase_auth_ktx",
      "com.google.guava:guava:$com_google_guava_guava",
      "com.google.protobuf:protobuf-javalite:$com_google_protobuf_protobuf_javalite",
      "nl.dionsegijn:konfetti:$nl_dionsegijn_konfetti",
      "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$org_jetbrains_kotlin_kotlin_stdlib_jdk7",
      "org.jetbrains.kotlinx:kotlinx-coroutines-core:$org_jetbrains_kotlinx_kotlinx_coroutines_core",
      "org.jetbrains.kotlinx:kotlinx-coroutines-android:$org_jetbrains_kotlinx_kotlinx_coroutines_android",
      "org.mockito:mockito-core:$org_mockito_mockito_core",
      "com.github.oppia:android-spotlight:$com_github_oppia_android_spotlight"
  )
  compileOnly(
      "jakarta.xml.bind:jakarta.xml.bind-api:$jakarta_xml_bind_jakarta_xml_bind_api",
      "javax.annotation:javax.annotation-api:$javax_annotation_javax_annotation_api",
      "org.glassfish.jaxb:jaxb-runtime:$org_glassfish_jaxb_jaxb_runtime",
  )
  testImplementation(
      "androidx.compose.ui:ui-test-junit4:$androidx_compose_ui_ui_test_junit4",
      "androidx.test:core:$androidx_test_core",
      "androidx.test.espresso:espresso-contrib:$androidx_test_espresso_espresso_contrib",
      "androidx.test.espresso:espresso-core:$androidx_test_espresso_espresso_core",
      "androidx.test.espresso:espresso-intents:$androidx_test_espresso_espresso_intents",
      "androidx.test.ext:junit:$androidx_test_ext_junit",
      "androidx.test.ext:truth:$androidx_test_ext_truth",
      "androidx.work:work-testing:$androidx_work_work_testing",
      "com.github.bumptech.glide:mocks:$com_github_bumptech_glide_mocks",
      "com.google.truth:truth:$com_google_truth_truth",
      "com.google.truth.extensions:truth-liteproto-extension:$com_google_truth_extensions_truth_liteproto_extension",
      "org.robolectric:annotations:$org_robolectric_annotations",
      "org.robolectric:robolectric:$org_robolectric_robolectric",
      "org.jetbrains.kotlinx:kotlinx-coroutines-test:$org_jetbrains_kotlinx_kotlinx_coroutines_test",
      "org.jetbrains.kotlin:kotlin-test-junit:$org_jetbrains_kotlin_kotlin_test_junit",
      "org.mockito:mockito-core:$org_mockito_mockito_core",
      project(":testing"),
  )
  androidTestImplementation(
      "androidx.compose.ui:ui-test-junit4:$androidx_compose_ui_ui_test_junit4",
      "androidx.test:core:$androidx_test_core",
      "androidx.test.espresso:espresso-contrib:$androidx_test_espresso_espresso_contrib",
      "androidx.test.espresso:espresso-core:$androidx_test_espresso_espresso_core",
      "androidx.test.espresso:espresso-intents:$androidx_test_espresso_espresso_intents",
      "androidx.test.ext:junit:$androidx_test_ext_junit",
      "androidx.test.ext:truth:$androidx_test_ext_truth",
      "com.github.bumptech.glide:mocks:$com_github_bumptech_glide_mocks",
      "com.google.truth:truth:$com_google_truth_truth",
      "androidx.work:work-testing:$androidx_work_work_testing",
      "com.google.truth.extensions:truth-liteproto-extension:$com_google_truth_extensions_truth_liteproto_extension",
      "org.jetbrains.kotlinx:kotlinx-coroutines-test:$org_jetbrains_kotlinx_kotlinx_coroutines_test",
      "org.mockito:mockito-android:$org_mockito_mockito_android",
      "org.robolectric:annotations:$org_robolectric_annotations",
  )
  // Adding the testing module directly causes duplicates of the below groups so we need to
  // exclude them before adding the testing module to the androidTestImplementation dependencies
  androidTestImplementation(project(":testing")) {
    exclude group: 'org.apache.maven', module: 'maven-artifact'
    exclude group: 'org.apache.maven', module: 'maven-artifact-manager'
    exclude group: 'org.apache.maven', module: 'maven-model'
    exclude group: 'org.apache.maven', module: 'maven-plugin-registry'
    exclude group: 'org.apache.maven', module: 'maven-profile'
    exclude group: 'org.apache.maven', module: 'maven-project'
    exclude group: 'org.apache.maven', module: 'maven-settings'
    exclude group: 'org.apache.maven', module: 'maven-error-diagnostics'
    exclude group: 'org.apache.maven', module: 'maven-ant-tasks'
    exclude group: "org.apache.maven.wagon"
    exclude group: "org.codehaus.plexus"
    exclude group: "org.robolectric"
  }
  androidTestUtil(
      "androidx.test:orchestrator:$androidx_test_orchestrator",
  )
  kapt(
      "com.google.dagger:dagger-compiler:$com_google_dagger_dagger_compiler"
  )
  kaptTest(
      "com.google.dagger:dagger-compiler:$com_google_dagger_dagger_compiler"
  )
  kaptAndroidTest(
      "com.google.dagger:dagger-compiler:$com_google_dagger_dagger_compiler"
  )
  // TODO(#59): Remove this once Bazel is introduced
  api project(':data')
  implementation project(":model")
  testImplementation project(":model")
  androidTestImplementation project(":model")
  implementation project(":domain")
  implementation project(":utility")
}
// The GeneratedMessageLite implementations of protobufs are depending on protobuf-java
// instead of protobuf-lite after Android Studio 3.5,
// The below command stops that from happening: https://github.com/google/tink/issues/282
configurations {
  all*.exclude module: 'protobuf-java'
}

def _computeTestsCorrespondingToShard(shardCount, shardIndex, exclude) {
  def allAppTestDirs = android.sourceSets.test.java.source +
          android.sourceSets.test.kotlin.source +
          android.sourceSets.androidTest.java.source +
          android.sourceSets.androidTest.kotlin.source
  def allAppTestFiles = []
  // https://stackoverflow.com/a/38899519/3689782 for file traversal in Groovy.
  allAppTestDirs.unique().each { testSrcDir ->
    def testSrcDirFile = testSrcDir instanceof File ? testSrcDir : new File(testSrcDir)
    if (!testSrcDirFile.isAbsolute()) {
      testSrcDirFile = new File(projectDir, testSrcDirFile.path).getAbsoluteFile()
    }
    if (testSrcDirFile.exists()) {
      testSrcDirFile.traverse(type: groovy.io.FileType.FILES, nameFilter: ~/^.+?Test\.kt$/) { it ->
        allAppTestFiles += new File(it.path).getAbsoluteFile()
      }
    }
  }
  def testFilePaths = allAppTestFiles.collect {
    projectDir.toPath().relativize(it.toPath()).toFile().path
  }.unique()

  // Use Random so that the division of tests is relatively even (using just the file path hash
  // alone can result in some skew which may hamper performance). Since the random class is being
  // reseeded this should be deterministic. It should also be consistent across different platforms
  // since only relative paths are used.
  def random = new Random()
  return testFilePaths.findAll {
    random.seed = it.hashCode()
    def rolledIndex = random.nextInt(shardCount)
    exclude ? rolledIndex != shardIndex : rolledIndex == shardIndex
  }.sort()
}

def _excludeSourceFiles(filesToExclude) {
  tasks.withType(SourceTask.class).configureEach {
    it.exclude(filesToExclude)
  }
  android.sourceSets.test.java.exclude(filesToExclude)
  android.sourceSets.test.kotlin.exclude(filesToExclude)
  android.sourceSets.androidTest.java.exclude(filesToExclude)
  android.sourceSets.androidTest.kotlin.exclude(filesToExclude)
}
