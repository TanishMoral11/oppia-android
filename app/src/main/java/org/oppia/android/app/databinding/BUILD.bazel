load("//defs:generate_android_manifest.bzl", "generate_android_manifest")

_DATABINDING_PACKAGE = "org.oppia.android"

genrule(
    name = "_generate_data_binder_mapper_impl",
    srcs = [],
    outs = ["DataBinderMapperImpl.java"],
    cmd = """
    echo "package androidx.databinding;" > \"$@\"
    echo "public final class DataBinderMapperImpl extends MergedDataBinderMapper {" >> \"$@\"
    echo "  DataBinderMapperImpl() { addMapper(new %s.DataBinderMapperImpl()); }" >> \"$@\"
    echo "}" >> \"$@\"
    """ % _DATABINDING_PACKAGE,
)

# TODO(#1639): Translate binding adapters back to Kotlin post-Bazel modularization.
android_library(
    name = "databinding_resources",
    srcs = [
        "AppCompatCheckBoxBindingAdapters.java",
        "CircularProgressIndicatorAdapters.java",
        "ColorBindingAdapters.java",
        "ConstraintLayoutAdapters.java",
        "DataBinderMapperImpl.java",
        "DrawableBindingAdapters.java",
        "EditTextBindingAdapters.java",
        "GuidelineBindingAdapters.java",
        "ImageViewBindingAdapters.java",
        "MarginBindingAdapters.java",
        "RecyclerViewBindingAdapter.java",
        "StateAssemblerMarginBindingAdapters.java",
        "StateAssemblerPaddingBindingAdapters.java",
        "TextInputLayoutBindingAdapters.java",
        "TextViewBindingAdapters.java",
        "ViewBindingAdapters.java",
    ],
    custom_package = _DATABINDING_PACKAGE,
    enable_data_binding = True,
    exports_manifest = False,
    manifest = generate_android_manifest(
        name = "databinding_resources",
        min_sdk_version = 19,
        package = _DATABINDING_PACKAGE,
        target_sdk_version = 31,
    ),
    resource_files = ["//app:databinding_layouts"],
    visibility = [
        "//:oppia_binary_visibility",
        "//app:__pkg__",
        "//app:app_testing_visibility",
        "//app/src/main/java/org/oppia/android/app/shim:__pkg__",
        "//app/src/main/java/org/oppia/android/app/testing/activity:__pkg__",
        "//instrumentation:__pkg__",
    ],
    deps = [
        "//app:annotations",
        "//app:listeners",
        "//app:resources",
        "//app:view_models",
        "//app:views",
        "//app/src/main/java/org/oppia/android/app/spotlight",
        "//app/src/main/java/org/oppia/android/app/translation:app_language_activity_injector_provider",
        "//app/src/main/java/org/oppia/android/app/translation:app_language_resource_handler",
        "//model/src/main/proto:event_logger_java_proto_lite",
        "//model/src/main/proto:interaction_object_java_proto_lite",
        "//model/src/main/proto:languages_java_proto_lite",
        "//model/src/main/proto:profile_java_proto_lite",
        "//model/src/main/proto:thumbnail_java_proto_lite",
        "//model/src/main/proto:topic_java_proto_lite",
        "//model/src/main/proto:translation_java_proto_lite",
        "//model/src/main/proto:version_java_proto_lite",
        "//third_party:androidx_annotation_annotation",
        "//third_party:androidx_appcompat_appcompat",
        "//third_party:androidx_constraintlayout_constraintlayout",
        "//third_party:androidx_core_core",
        "//third_party:androidx_databinding_databinding-adapters",
        "//third_party:androidx_databinding_databinding-common",
        "//third_party:androidx_databinding_databinding-runtime",
        "//third_party:androidx_drawerlayout_drawerlayout",
        "//third_party:androidx_lifecycle_lifecycle-livedata-core",
        "//third_party:androidx_lifecycle_lifecycle-livedata-ktx",
        "//third_party:androidx_recyclerview_recyclerview",
        "//third_party:androidx_viewpager2_viewpager2",
        "//third_party:androidx_viewpager_viewpager",
        "//third_party:circularimageview_circular_image_view",
        "//third_party:com_github_bumptech_glide_glide",
        "//third_party:com_google_android_flexbox_flexbox",
        "//third_party:com_google_android_material_material",
        "//third_party:de_hdodenhof_circleimageview",
        "//third_party:javax_annotation_javax_annotation-api",
        "//third_party:nl_dionsegijn_konfetti",
        "//utility/src/main/java/org/oppia/android/util/logging:sync_status_manager",
        "//utility/src/main/java/org/oppia/android/util/system:oppia_clock",
        "//utility/src/main/java/org/oppia/android/util/system:oppia_clock_injector_provider",
    ],
)
