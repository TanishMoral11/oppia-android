apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion target_sdk_version
  buildToolsVersion build_tools_version

  defaultConfig {
    minSdkVersion min_sdk_version
    targetSdkVersion target_sdk_version
    versionCode 1
    versionName "1.0"
    javaCompileOptions {
      annotationProcessorOptions {
        includeCompileClasspath true
      }
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
      all {
        // Enable forking to ensure each test is properly run in isolation. For context, see:
        // https://discuss.gradle.org/t/36066/2 & https://github.com/oppia/oppia-android/issues/1942
        forkEvery = 1
        maxParallelForks = Runtime.getRuntime().availableProcessors()

        // https://discuss.gradle.org/t/29495/2 & https://stackoverflow.com/a/34299238.
        testLogging {
          events "passed", "skipped", "failed"
          showExceptions = true
          exceptionFormat = "full"
          showCauses = true
          showStackTraces = true
          showStandardStreams = false
        }
      }
    }
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
}

// These tests aren't supported in Gradle since they make use of test resources & AGP doesn't
// support merging resources for test builds, or they rely on a compile-time built proto
// configuration file which the current Gradle configuration doesn't support. This test runs
// correctly for Bazel & is included in the CI workflow that runs Bazel tests.
// https://stackoverflow.com/a/69141612 seems like the only solution that actually works (versus
// trying to exclude via sourceSets), so the following is an adapted version that ensures all
// generated sources that may reference the test also don't exist (such as Dagger running to
// generate a test application component). Note that this must exist in tandem with the sourceSet
// exclusion in order to properly work.
def filesToExclude = [
    '**/*DisplayLocaleImplTest*.kt'
]
tasks.withType(SourceTask.class).configureEach {
  it.exclude(filesToExclude)
}
android.sourceSets.test.java.exclude(filesToExclude)
android.sourceSets.test.kotlin.exclude(filesToExclude)

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation(
      "androidx.appcompat:appcompat:$androidx_appcompat_appcompat",
      "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_lifecycle_livedata_ktx",
      "androidx.work:work-runtime-ktx:$androidx_work_work_runtime_ktx",
      "com.github.oppia:androidsvg:$com_github_oppia_androidsvg",
      "com.github.oppia:kotlitex:$com_github_oppia_kotlitex",
      "com.github.bumptech.glide:glide:$com_github_bumptech_glide_glide",
      "com.google.dagger:dagger:$com_google_dagger_dagger",
      "com.google.firebase:firebase-analytics-ktx:$com_google_firebase_firebase_analytics_ktx",
      "com.google.firebase:firebase-crashlytics:$com_google_firebase_firebase_crashlytics",
      "com.google.firebase:firebase-firestore-ktx:$com_google_firebase_firebase_firestore_ktx",
      "com.google.firebase:firebase-auth-ktx:$com_google_firebase_firebase_auth_ktx",
      "com.google.protobuf:protobuf-javalite:$com_google_protobuf_protobuf_javalite",
      "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$org_jetbrains_kotlin_kotlin_stdlib_jdk8",
      "org.jetbrains.kotlinx:kotlinx-coroutines-core:$org_jetbrains_kotlinx_kotlinx_coroutines_core",
  )
  compileOnly(
      "jakarta.xml.bind:jakarta.xml.bind-api:$jakarta_xml_bind_jakarta_xml_bind_api",
      "javax.annotation:javax.annotation-api:$javax_annotation_javax_annotation_api",
      "org.glassfish.jaxb:jaxb-runtime:$org_glassfish_jaxb_jaxb_runtime",
  )
  testImplementation(
      "androidx.test.ext:junit:$androidx_test_ext_junit",
      "androidx.test.ext:truth:$androidx_test_ext_truth",
      "com.google.dagger:dagger:$com_google_dagger_dagger",
      "com.google.truth:truth:$com_google_truth_truth",
      "com.google.truth.extensions:truth-liteproto-extension:$com_google_truth_extensions_truth_liteproto_extension",
      "junit:junit:$junit_junit",
      "org.jetbrains.kotlin:kotlin-test-junit:$org_jetbrains_kotlin_kotlin_test_junit",
      "org.jetbrains.kotlinx:kotlinx-coroutines-test:$org_jetbrains_kotlinx_kotlinx_coroutines_test",
      "org.mockito:mockito-core:$org_mockito_mockito_core",
      "org.robolectric:robolectric:$org_robolectric_robolectric",
      project(":data"),
      project(":testing"),
  )
  kapt(
      "com.github.bumptech.glide:compiler:$com_github_bumptech_glide_compiler",
      "com.google.dagger:dagger-compiler:$com_google_dagger_dagger_compiler"
  )
  kaptTest(
      "com.google.dagger:dagger-compiler:$com_google_dagger_dagger_compiler"
  )
  // TODO(#59): Remove this once Bazel is introduced
  // sufficiently visible for generated Dagger code. This can be done more cleanly via Bazel since dependencies can be
  // controlled more directly than in Gradle.
  implementation project(':model')
}
// The GeneratedMessageLite implementations of protobufs are depending on protobuf-java
// instead of protobuf-lite after Android Studio 3.5,
// The below command stops that from happening: https://github.com/google/tink/issues/282
configurations {
  all*.exclude module: 'protobuf-java'
}
